buildscript {
    repositories {
        jcenter()
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven {
            name "forge"
            url "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

defaultTasks 'fullBuild'

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name 'amadornes'
        url 'https://maven.amadornes.com/'
    }
    maven {
        name 'prog'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'tesla'
        url 'https://maven.mcmoddev.com'
    }
    maven {
        name 'curseforge'
        url 'https://minecraft.curseforge.com/api/maven/'
    }
    maven {
        name 'opencomputers'
        url 'https://maven.cil.li/'
    }
    maven {
        name 'CoFH Maven'
        url 'https://maven.covers1624.net'
    }
    maven {
        name 'buildcraft'
        url 'https://mod-buildcraft.com/maven/'
    }
    maven {
        name 'tterrag'
        url 'https://maven.tterrag.com/'
    }
    maven {
        name 'ic2'
        url 'https://maven.ic2.player.to'
    }
    maven {
        name 'BlameJared - Crafttweaker'
        url 'https://maven.blamejared.com/'
    }
    maven {
        name 'thiakil'
        url 'https://maven.thiakil.com'
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

group = "igentuman"
version = "${minecraft_version}-${mod_version}"
archivesBaseName = "BetterFusionReactor"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

def explodedVersion = mod_version.split("\\.")

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${mappings_version}"
    runDir = "run"
    useDepAts = true

    replace '${version}', project.version
    replace 'new Version(999, 999, 999)', "new Version(${explodedVersion[0]}, ${explodedVersion[1]}, ${explodedVersion[2]})"
    replaceIn "BetterFusionReactor.java"
}

dependencies {
    deobfCompile "mekanism:Mekanism:${mekanism_version}"
    deobfCompile "mekanism-generators:MekanismGenerators:${mekanism_version}"
    compileOnly "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    compileOnly "net.darkhax.tesla:Tesla-${minecraft_version}:${tesla_version}"
    compileOnly "inventory-tweaks:InventoryTweaks:${invtweak_version}:api"
    deobfCompile "li.cil.oc:OpenComputers:MC${minecraft_version}-${opencomputers_version}:api"

    compileOnly "com.mod-buildcraft:buildcraft-api:${buildcraft_version}"
    compileOnly "team.chisel.ctm:CTM:MC${minecraft_version}-${ctm_version}:api"
    runtime "team.chisel.ctm:CTM:MC${minecraft_version}-${ctm_version}"
    compileOnly "net.industrial-craft:industrialcraft-2:${ic2_version}:api"
    compileOnly("CraftTweaker2:CraftTweaker2-API:${crafttweaker_version}") {
        exclude module: "asm-debug-all"
    }
    deobfCompile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crafttweaker_version}") {
        exclude module: "asm-debug-all"
    }
    deobfCompile "dan200.computercraft:ComputerCraft:${computercraft_version}:api"
    compileOnly "mcjty.theoneprobe:TheOneProbe-1.12:${top_version}:api"
    deobfCompile "cofh:RedstoneFlux:1.12-+:universal"
    deobfCompile "MCMultiPart2:MCMultiPart:${mcmp_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mc_version", minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mc_version": minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude '**/*.blend*'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

task bfrJar(type: Jar) {
    archiveName = "BFR-${project.version}.jar"
    dependsOn 'reobfJar'
    classifier "bfr"

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'igentuman/bfr/**', 'assets/bfr/**'
    }
}


artifacts {
    archives bfrJar
}

task fullBuild(type: Delete) {
    delete jar
    dependsOn "build"
}

